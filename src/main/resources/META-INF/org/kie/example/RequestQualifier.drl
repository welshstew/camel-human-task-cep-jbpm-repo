package org.kie.example.project1;

rule "Mark an error as critical when there are 5 or more errors in the 10 seconds"
ruleflow-group "Validation"
salience 1
when
    $requestQualifier: RequestQualifier(processed == false)
    Number( intValue >=5 ) from accumulate($r: RequestQualifier( ) over window:time( 10s ),count( $r ) )
    Number( intValue ==0 ) from accumulate($r: RequestQualifier( critical == true) over window:time(10m ),count( $r ) )
then
   	System.out.println("Request qualified as critical: " + $requestQualifier);
    modify($requestQualifier) {
        setProcessed(true),
        setCritical(true);
    }
end

rule "Mark RequestQualifiers as processed"
ruleflow-group "Validation"
salience 0

when
    $requestQualifier: RequestQualifier(processed == false)
then
   	System.out.println("Mark non critical request as processed: " + $requestQualifier);
    modify($requestQualifier) { setProcessed(true) }
end